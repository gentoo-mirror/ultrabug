# Copyright 1999-2010 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

EAPI=3

inherit base

DESCRIPTION="SMTP proxy for signing/verifying DKIM and Domainkey signatures"
HOMEPAGE="http://dkimproxy.sourceforge.net/"
SRC_URI="http://downloads.sourceforge.net/${PN}/${P}.tar.gz"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~amd64 ~x86"
IUSE=""

RDEPEND="dev-perl/Mail-DKIM
	dev-perl/net-server
	dev-perl/Error
"
DEPEND="${RDEPEND}"

pkg_setup() {
	# create a dkfilter user for enhanced security
	enewgroup dkfilter
	enewuser dkfilter -1 -1 /dev/null dkfilter
}

src_install() {
	base_src_install

	dodoc AUTHORS README || die
	newinitd "${FILESDIR}/dkimproxy.out-initd" dkimproxy.out || die
	newinitd "${FILESDIR}/dkimproxy.in-initd" dkimproxy.in || die
	newconfd "${FILESDIR}/dkimproxy.out-confd" dkimproxy.out || die
	newconfd "${FILESDIR}/dkimproxy.in-confd" dkimproxy.in || die

	rm -f "${D}/etc/dkimproxy_in.conf.example" || die
	rm -f "${D}/etc/dkimproxy_out.conf.example" || die
	insinto /etc
	newins "${FILESDIR}/dkimproxy_in.conf.gentoo" dkimproxy_in.conf || die
	newins "${FILESDIR}/dkimproxy_out.conf.gentoo" dkimproxy_out.conf || die
 	keepdir /etc/ssl/${PN}
}

pkg_postinst() {
	CONFDIR="/etc/ssl/${PN}"
	if [[ ! -f "${CONFDIR}/dkimproxy.private.key" ]]; then
		openssl genrsa -out "${CONFDIR}/dkimproxy.private.key" 1024 &>/dev/null || die
		openssl rsa -in "${CONFDIR}/dkimproxy.private.key" -out "${CONFDIR}/dkimproxy.public.key" -pubout -outform PEM || die
		chown root:dkfilter "${CONFDIR}/dkimproxy.private.key" "${CONFDIR}/dkimproxy.public.key"
		chmod 0640 "${CONFDIR}/dkimproxy.private.key" "${CONFDIR}/dkimproxy.public.key"
		elog "Autogenerated sample signing keys in ${CONFDIR}."
	fi
}
